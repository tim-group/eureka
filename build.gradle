buildscript {
    repositories {
        mavenLocal()
        if (project.hasProperty('repoUrl')) {
            mavenRepo url: "${project.repoUrl}/groups/public"
        }
        else {
            mavenCentral()
        }
    }
    dependencies {
        classpath 'com.kenshoo:gradle-fpm:0.4'
    }
}

repositories {
    mavenLocal()
    if (project.hasProperty('repoUrl')) {
        mavenRepo url: "${project.repoUrl}/groups/public"
    }
    else {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'fpm-packaging'

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

dependencies {
    compile 'com.google.guava:guava:15.0'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'org.simpleframework:simple:5.1.6'

    testCompile 'junit:junit:4.11'
}

project.ext.iteration = System.getenv("BUILD_NUMBER")

if (!project.ext.iteration) {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "--short", "HEAD"
        standardOutput = stdout
    }

    project.ext.iteration = "$stdout".trim()
}

packaging {
    dependencies = []
    baseDir      = "$buildDir/libs"
    prefix       = '/opt/eureka'
    extraOptions = [
        '--name': 'timgroup-eureka',
        '--maintainer': 'Francesco Gigli <francesco.gigli@timgroup.com>',
        '--architecture': 'all',
        '--version': '0.0.1',
        '--iteration': project.ext.iteration,
        '--url': 'https://github.com/youdevise/eureka',
        '--description': 'reactive monitoring for production'
    ]
    filesArgs    = ['eureka.jar']
    force        = false
}

debian.dependsOn jar
